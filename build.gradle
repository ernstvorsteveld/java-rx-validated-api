plugins {
    id 'java'
    id 'org.springframework.boot' version '3.1.2'
    id 'io.spring.dependency-management' version '1.1.2'
    id("org.openapi.generator") version "6.6.0"

}

group = 'com.sternitc'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_17
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

configurations.all {
    exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
    exclude group: 'org.springframework.boot', module: 'logback-classic'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.openapitools:openapi-generator-gradle-plugin:6.6.0"
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'io.swagger.core.v3:swagger-annotations:2.2.15'
    implementation 'io.swagger:swagger-annotations:2.0.0-rc2'
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    implementation 'javax.validation:validation-api:2.0.1.Final'
    implementation 'org.openapitools:jackson-databind-nullable:0.2.6'
    implementation 'org.mapstruct:mapstruct:1.5.5.Final'
    implementation 'com.github.java-json-tools:json-patch:1.13'
    implementation 'com.jayway.jsonpath:json-path:2.8.0'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.15.2'
//    implementation 'org.apache.johnzon:johnzon-core:1.2.21'
//    implementation 'javax.json:javax.json-api:1.1.4'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'


    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.projectreactor:reactor-test'
}

apply plugin: 'org.openapi.generator'

compileJava.dependsOn tasks.named("openApiGenerate")

sourceSets {
    main {
        java {
            srcDir file("$buildDir/generated/src/main/java")
        }
    }
}

tasks.named('test') {
    useJUnitPlatform()
}


openApiGenerate {
    generatorName.set("spring")
    inputSpec.set("$projectDir/src/main/spec/api.yaml")
    outputDir.set("$buildDir/generated/")
    apiPackage.set("com.sternitc.generated.api")
    modelPackage.set("com.sternitc.generated.model")
    configOptions.set([
            "delegatePattern"           : "true",
            "reactive"                  : "true",
            "generateApis"              : "false",
            "interfaceOnly"             : "true"
    ])
    globalProperties.set([
            "dateLibrary"               : "java8",
            "generateApiTests"          : "false",
            "generateModels"            : "true",
            "generateModelTests"        : "false",
            "generateModelDocumentation": "false",
            "generateApiDocumentation"  : "false",
            "generateSupportingFiles"   : "false",
            "hideGenerationTimestamp"   : "true",
            "library"                   : "spring-boot",
            "useSpringController"       : "true",
            "useSpringfox"              : "false",
            "serializableModel"         : "true",
            "useBeanValidation"         : "true",
            "performBeanValidation"     : "true",
            "useTags"                   : "true",
            "implicitHeaders"           : "true",
            "openApiNullable"           : "false",
            "oas3"                      : "true"
    ])
}
